# Find products for a given set of alternative features

## Description

A consumer is looking for a product and has a general idea about what he wants, products showing one or more alternative features. This one is an alteration of Benchmark 1a that makes the query less specific by considering the requested features alternatives; the user is interested in any product that presents at least one of them. This way we can benchmark how exploration tools deal with letting users define the OR operator.

The corresponding information need is about a randomly selected product type from the product hierarchy (one level above leaf level), two different randomly selected product features that at least one of them corresponds to the chosen product and a number between 1 and 500 for a numeric property.

## Sample

> "List products attributes for product **sheeny** with product features **stroboscopes** OR **gadgeteers**, and a **productPropertyNumeric1** greater than **450**".

### Expected Outcome
Given the sample dataset [bsbm-1000products](../Datasets/bsbm-1000products.ttl.tgz):

> “aliter tiredest”, “auditoriums reducing pappies”, “boozed”, “byplay”, “closely jerries”, “cogitations centralest recastin”, “driveled”, “elatedly fidelis release”, “fluoridations minutely”, “gauls”, “imposed”, “intangibilities daleths silverfish”, “learnable onomatopoeically”, “retranslating snuffing”, “whizbangs” and “zellations”.

## SPARQL Query to Perform

```SPARQL
SELECT DISTINCT ?product ?label
WHERE {
	?product rdfs:label ?label .
	?product a %ProductType% .
	?product bsbm:productFeature ?feature .
	FILTER (?feature = %ProductFeature1% ||
		?feature = %ProductFeature2% )
	?product bsbm:productPropertyNumeric1 ?value1 .
	FILTER (?value1 > %x%)
}
```

### SPARQL Query for the Sample Query

```SPARQL
PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?product ?label
WHERE {
	?product rdfs:label ?label .
	?product a/rdfs:subClassOf? ?sheenyType .
	?sheenyType rdfs:label "sheeny" .
	?product bsbm:productFeature ?feature .
	?stroboscopes rdfs:label "stroboscopes" .
	?gadgeteers rdfs:label "gadgeteers" .
	FILTER (?feature = ?stroboscopes ||
		?feature = ?gadgeteers )
	?product bsbm:productPropertyNumeric1 ?value1 .
	FILTER (?value1 > 450)
}
```
